#!perl

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;

use Getopt::Long::Complete qw(GetOptionsWithCompletion);
use Time::HiRes 'sleep';

my $PROG = "spanel-tail-weblog";

my $Site;
my %Opts = (
    http => 1,
    https => 1,
);

sub parse_options {
    GetOptionsWithCompletion(
        sub {
            my %args = @_;
            my $word = $args{word};
            my $type = $args{type};
            if ($type eq 'arg') {
                require Complete::Spanel;
                return Complete::Spanel::complete_spanel_site(
                    word => $word,
                );
            }
            [];
        },
        'help|h|?' => sub {
            print "Usage: $PROG [options]\n";
            print <<EOT;
Usage: $PROG [options] <site>
Options:
  --no-http     Don't tail HTTP log.
  --no-https    Don't tail HTTPS log.
For more details, see the documentation (man $PROG).
EOT
            exit 0;
        },
        'version|v' => sub {
            no warnings 'once';
            print "$PROG version ", ($main::VERSION // "dev"), "\n";
            exit 0;
        },
        'http!' => \$Opts{http},
        'https!' => \$Opts{https},
        '<>' => sub {
            die "$PROG: Extraneous argument '$_[0]'\n" if defined $Site;
            $_[0] =~ /\A[\w-]+(\.[\w-]+)*\z/
                or die "$PROG: Invalid site '$_[0]'\n";
            $Site = $_[0];
        },
    );

    die "$PROG: Please specify site\n" unless $Site;
    my $dir = "/s/$Site/syslog";
    (-d $dir) or die "$PROG: Site not found: '$Site': no $dir\n";
    chdir $dir or die "$PROG: Can't chdir to '$dir'\n";
}

sub run {
    require Logfile::Tail::Switch;

    no warnings 'once';
    local $Logfile::Tail::Switch::WARN_NO_MATCH = 0;

    my @globs;
    if ($Opts{http})  { push @globs, "http_access.*.log"  }
    if ($Opts{https}) { push @globs, "https_access.*.log" }

    my $tail = Logfile::Tail::Switch->new(
        globs => \@globs,
        #check_freq => $Opts{check_freq},
        #tail_new   => $Opts{tail_new},
    );

    while (1) {
        my $line = $tail->getline;
        if (length $line) {
            print $line;
        } else {
            sleep 0.1;
        }
    }
}

# MAIN

$|++;
parse_options();
run();

# PODNAME:
# ABSTRACT: Tail -f spanel HTTP and/or HTTPS access log

=head1 SYNOPSIS

 % spanel-tail-weblog example.com            ; # both http & https
 % spanel-tail-weblog --no-https example.com ; # http only
 % spanel-tail-weblog --no-http  example.com ; # https only
